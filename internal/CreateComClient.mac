/*COMクライアントを作成する。

返値　成功：COM-Object(int)を文字列で返す
    　失敗：空文字
*/
/////////////////////////////////////////////////////////////////////////////
//    グローバル変数
/////////////////////////////////////////////////////////////////////////////

//
//LSP
//
$comClientFileName  =@"%HOMEDRIVE%%HOMEPATH%\GitHub\hidemaru_lsp_client\project\HidemaruLspClient_FrontEnd\bin\Debug\HidemaruLspClient_FrontEnd.dll";
$comServiceName     ="HidemaruLspClient_FrontEnd.Service";
$logFileName        =@"%HOMEDRIVE%%HOMEPATH%\hidemaru_lsp_client.log";
$comObjectName      ="lsp.client.obj";
$stageName          ="lsp.client.stage";
$configName         ="lsp.client.configname";
#comShareMode       =2;

$comError           ="";

$stageStart           ="";
$stageInitialized     ="Initialized";
$stageCreatedComObject="CreatedComObject";
$stageInitializedBackEndService="InitializedBackEndService";
$stageFindedConfig    ="FindedConfig";
$stageFinish          ="Finish";


//
//Iniファイル
//
$rootFolder     =currentmacrodirectory+@"\..";
$iniServerConfig=$rootFolder+"\\server_config.ini";


/////////////////////////////////////////////////////////////////////////////
//    メイン処理
/////////////////////////////////////////////////////////////////////////////
call main;
endmacro getstaticvariable($comObjectName,#comShareMode);


main:
    $$stage = getstaticvariable($stageName,#comShareMode);
    if($$stage==$stageStart){
        call InitializeGlobalVariable;
        if(! ##return){
            return ;
        }
        setstaticvariable $stageName,$stageInitialized,#comShareMode;
        call main;
        return ;

    }else if($$stage==$stageInitialized){
        call CreateComObject;
        if(! ##return){
            return ;
        }
        setstaticvariable $stageName,$stageCreatedComObject,#comShareMode;
        call main;
        return ;

    }else if($$stage==$stageCreatedComObject){
        call InitializeBackEndService;
        if(! ##return){
            return ;
        }
        setstaticvariable $stageName,$stageInitializedBackEndService,#comShareMode;
        call main;
        return;

    }else if($$stage==$stageInitializedBackEndService){
        call FindServerConfigFile;
        if(! ##return){
            return ;
        }
        setstaticvariable $stageName,$stageFindedConfig,#comShareMode;
        call main;
        return ;
    }else if($$stage==$stageFindedConfig){
        call InitializeFrontEndService;
        if(! ##return ){
            return false;
        }
        setstaticvariable $stageName,$stageFinish,#comShareMode;
        call main;
        return ;

    }else if($$stage==$stageFinish){
        return ;
    }
    return ;


FindServerConfigFile:
    $$extension = filetype;
    $$configFilename=getinistrw($iniServerConfig, "ServerConfig", $$extension);
    if($$configFilename==""){
        //message "言語サーバの設定がIniファイル中で見つかりません";
        return false;
    }
    $$absConfigFileName=$rootFolder+"\\"+$$configFilename;
    if(! existfile($$absConfigFileName)){
        message "ファイルが見つからない。"+$$absConfigFileName;
        return false;
    }
    setstaticvariable $configName,$$absConfigFileName,#comShareMode;
    return true;


InitializeGlobalVariable:
    execmacro currentmacrodirectory+@"\expand_environment_strings.mac", $comClientFileName;
    $comClientFileName=getresultex(-1);

    execmacro currentmacrodirectory+@"\expand_environment_strings.mac", $logFileName;
    $logFileName=getresultex(-1);

    call GetAbsolutePathName $iniServerConfig;
    $iniServerConfig=$$return;
    return true;


CreateComObject:
    ##obj = createobject($comClientFileName,$comServiceName);
    if(##obj==0){
        message "COMのロードに失敗しました";
        return false;
    }
    setstaticvariable $comObjectName,str(##obj),#comShareMode;
    setcomdetachmethod ##obj,"Finalizer";
    return true;

InitializeBackEndService:
    $$obj=getstaticvariable($comObjectName,#comShareMode);
    ##obj=val($$obj);
    ##n=member(##obj,"InitializeBackEndService",$logFileName);
    if(##n==false){
        message "[Failed] InitializeBackEndService";
        return false;
    }
    return true;

InitializeFrontEndService:
    $$obj=getstaticvariable($comObjectName,#comShareMode);
    ##obj=val($$obj);
    $$absConfigFileName=getstaticvariable($configName,#comShareMode);

    ##n=member(##obj,"InitializeFrontEndService",$$absConfigFileName,directory2);
    if(##n==false){
        message "[Failed] InitializeFrontEndService";
        return false;
    }
    ##serverCapabilities=member(##obj,"ServerCapabilities");
    execmacro currentmacrodirectory+@"/LspServerCapabilities.mac", "Store", str(##serverCapabilities);
    keepobject ##obj, 2;
    return true;


GetAbsolutePathName:
	##fso=createobject("Scripting.FileSystemObject");
	$$abs_filename = member(##fso,"GetAbsolutePathName",$$1);
	releaseobject ##fso;
    return $$abs_filename;
