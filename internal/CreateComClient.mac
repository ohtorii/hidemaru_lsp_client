/*COMクライアントを作成する。

返値　成功：COM-Object(int)を文字列で返す
    　失敗：空文字
*/
/////////////////////////////////////////////////////////////////////////////
//    グローバル変数
/////////////////////////////////////////////////////////////////////////////

#useAsync=true;

//
//LSP
//
$comClientFileName  =@"%HOMEDRIVE%%HOMEPATH%\GitHub\hidemaru_lsp_client\project\HidemaruLspClient_FrontEnd\bin\Debug\HidemaruLspClient_FrontEnd.dll";
$comService         =/*"{0B0A4550-A71F-4142-A4EC-BC6DF50B9590}";*/ "HidemaruLspClient_FrontEnd.Service";
$comServiceAsync    ="HidemaruLspClient_FrontEnd.ServiceAsync";
$comServiceName     ="";

$logFileName        =@"%HOMEDRIVE%%HOMEPATH%\hidemaru_lsp_client.log";
$privateComObjectName ="lsp.client.private.obj";
$publicComObjectName  ="lsp.client.public.obj";

#comShareMode       =2;

$labelIndexName="LabelIndex";
if(#useAsync){
    if(true){
        //非同期版
        $comServiceName=$comServiceAsync;
        $labelTable[0]="CreateComObject";
        $labelTable[1]="Initialize";
        $labelTable[2]="InitializeServiceAsync";
        $labelTable[3]="CheckService";
        $labelTable[4]="Finish";
        $labelTable[5]="Nop";
    }else{
        //疑似非同期版
        $comServiceName=$comService;
        $labelTable[0]="CreateComObject";
        $labelTable[1]="Initialize";
        $labelTable[2]="InitializeBackEndServiceAsync";
        $labelTable[3]="CheckBackEndService";
        $labelTable[4]="InitializeFrontEndServiceAsync";
        $labelTable[5]="CheckFrontEndService";
        $labelTable[6]="Finish";
        $labelTable[7]="Nop";
    }
}else{
    //同期版
    $comServiceName=$comService;
    $labelTable[0]="CreateComObject";
    $labelTable[1]="Initialize";
    $labelTable[2]="InitializeBackEndService";
    $labelTable[3]="InitializeFrontEndService";
    $labelTable[4]="Finish";
    $labelTable[5]="Nop";
}


//
//Iniファイル
//
$rootFolder     =currentmacrodirectory+@"\..";
$iniServerConfig=$rootFolder+"\\server_config.ini";


/////////////////////////////////////////////////////////////////////////////
//    メイン処理
/////////////////////////////////////////////////////////////////////////////
call main;
endmacro getstaticvariable($publicComObjectName,#comShareMode);

main:
    $$index = getstaticvariable($labelIndexName,#comShareMode);
    if($$index==""){
        ##index=0;
    }else{
        ##index=val($$index);
    }
    debuginfo 1;
    debuginfo $labelTable[##index];
    call $labelTable[##index];
    if(! ##return){
        return ;
    }

    ##index = ##index + 1;
    setstaticvariable $labelIndexName,str(##index),#comShareMode;
    call main;
    return ;

ShowComError:
    ##success=##1;
    ##hresult=##2;
    message sprintf("##success=%d / ##hresult=0x%x",##success, ##hresult);
    return ;

CreateComObject:
    execmacro currentmacrodirectory+@"\expand_environment_strings.mac", $comClientFileName;
    $$filename=getresultex(-1);
    ##obj = createobject($$filename,$comServiceName);
    ##success=getresultex(10);
    ##hresult=getresultex(11);
    if(##obj==0){
        message sprintf("COMのロードに失敗しました\n##obj=%d / ##success=%d / ##hresult=0x%x",##obj, ##success, ##hresult);
        return false;
    }
    setstaticvariable $privateComObjectName,str(##obj),#comShareMode;
    setcomdetachmethod ##obj,"Finalizer";
    keepobject ##obj, 2;
    return true;

Initialize:
    call GetAbsolutePathName $iniServerConfig;
    $$ini=$$return;

    $$obj=getstaticvariable($privateComObjectName,#comShareMode);
    ##obj=val($$obj);
    ##success=member(##obj,"Initialize",$$ini);
    if(! ##success){
        message "[Failed] Initialize"
        return false;
    }
    return true;

InitializeBackEndService:
    execmacro currentmacrodirectory+@"\expand_environment_strings.mac", $logFileName;
    $$log=getresultex(-1);

    $$obj=getstaticvariable($privateComObjectName,#comShareMode);
    ##obj=val($$obj);
    ##n=member(##obj,"InitializeBackEndService",$$log);
    if(##n==false){
        message "[Failed] InitializeBackEndService";
        return false;
    }
    return true;

InitializeBackEndServiceAsync:
    execmacro currentmacrodirectory+@"\expand_environment_strings.mac", $logFileName;
    $$log=getresultex(-1);

    $$obj=getstaticvariable($privateComObjectName,#comShareMode);
    ##obj=val($$obj);
    ##_=member(##obj,"InitializeBackEndServiceAsync",$$log);
    return true;

InitializeServiceAsync:
    execmacro currentmacrodirectory+@"\expand_environment_strings.mac", $logFileName;
    $$log=getresultex(-1);

    $$obj=getstaticvariable($privateComObjectName,#comShareMode);
    ##obj=val($$obj);
    /*##_=member(##obj,"SetFileType",filetype);
    ##_=member(##obj,"SetSourceCodeDirectory",directory2);*/
    ##_=member(##obj,"InitializeServiceAsync",$$log,filetype,directory2);
    return true;

CheckService:
    $$obj=getstaticvariable($privateComObjectName,#comShareMode);
    ##obj=val($$obj);
    return member(##obj,"CheckService");

CheckBackEndService:
    $$obj=getstaticvariable($privateComObjectName,#comShareMode);
    ##obj=val($$obj);
    return member(##obj,"CheckBackEndService");

InitializeFrontEndService:
    $$obj=getstaticvariable($privateComObjectName,#comShareMode);
    ##obj=val($$obj);
    ##n=member(##obj,"InitializeFrontEndService",filetype,directory2);
    if(##n==false){
        message "[Failed] InitializeFrontEndService";
        return false;
    }
    return true;

InitializeFrontEndServiceAsync:
    $$obj=getstaticvariable($privateComObjectName,#comShareMode);
    ##obj=val($$obj);
    ##_=member(##obj,"InitializeFrontEndServiceAsync",filetype,directory2);
    return true;

CheckFrontEndService:
    $$obj=getstaticvariable($privateComObjectName,#comShareMode);
    ##obj=val($$obj);
    return member(##obj,"CheckFrontEndService");

Finish:
    $$obj=getstaticvariable($privateComObjectName,#comShareMode);
    setstaticvariable $publicComObjectName,$$obj,#comShareMode;
    return true;

Nop:
    return false;


GetAbsolutePathName:
	##fso=createobject("Scripting.FileSystemObject");
	$$abs_filename = member(##fso,"GetAbsolutePathName",$$1);
	releaseobject ##fso;
    return $$abs_filename;
