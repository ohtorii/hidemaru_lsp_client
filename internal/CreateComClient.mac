/*COMクライアントを作成する。

返値　成功：COM-Object(int)を文字列で返す
    　失敗：空文字
*/
/////////////////////////////////////////////////////////////////////////////
//    グローバル変数
/////////////////////////////////////////////////////////////////////////////

//
//LSP
//
$comClientFileName  =@"%HOMEDRIVE%%HOMEPATH%\GitHub\hidemaru_lsp_client\project\HidemaruLspClient_FrontEnd\bin\Debug\HidemaruLspClient_FrontEnd.dll";
$comServiceName     ="HidemaruLspClient_FrontEnd.Service";
$logFileName        =@"%HOMEDRIVE%%HOMEPATH%\hidemaru_lsp_client.log";
$comObjectName      ="lsp.client.obj";
$stageName          ="lsp.client.stage";
$configName         ="lsp.client.configname";
#comShareMode       =2;

$comError           ="";

$stageStart           ="";
$stageInitialized     ="Initialized";
$stageCreatedComObject="CreateedComObject";
$stageFindedConfig    ="FindedConfig";
$stageFinish          ="Finish";


//
//Iniファイル
//
$rootFolder     =currentmacrodirectory+@"\..";
$iniServerConfig=$rootFolder+"\\server_config.ini";


/////////////////////////////////////////////////////////////////////////////
//    メイン処理
/////////////////////////////////////////////////////////////////////////////
call main;
endmacro $$return;


main:
    $$stage = getstaticvariable($stageName,#comShareMode);
    if($$stage==$stageStart){
        call InitializeGlobalVariable;
        setstaticvariable $stageName,$stageInitialized,#comShareMode;
        call main;
        return $$return;

    }else if($$stage==$stageInitialized){
        call CreateComObject;
        ##obj=##return;
        if(##obj==0){
            return $comError;
        }
        setstaticvariable $stageName,$stageCreatedComObject,#comShareMode;
        setstaticvariable $comObjectName,str(##obj),#comShareMode;
        call main;
        return $$return;

    }else if($$stage==$stageCreatedComObject){
        call FindServerConfigFile filetype;
        $$absConfigFileName=$$return;
        if($$absConfigFileName==""){
            return $comError;
        }
        setstaticvariable $configName,$$absConfigFileName,#comShareMode;
        setstaticvariable $stageName,$stageFindedConfig,#comShareMode;
        call main;
        return $$return;

    }else if($$stage==$stageFindedConfig){
        $$obj              =getstaticvariable($comObjectName,#comShareMode);
        $$absConfigFileName=getstaticvariable($configName,#comShareMode);
        call InitializeComObject val($$obj),$$absConfigFileName;
        if(##return == false){
            return $comError;
        }
        setstaticvariable $stageName,$stageFinish,#comShareMode;
        call main;
        return $$return;

    }else if($$stage==$stageFinish){
        return getstaticvariable($comObjectName,#comShareMode);
    }
    return $comError;


FindServerConfigFile:
    $$extension = $$1;
    $$configFilename=getinistrw($iniServerConfig, "ServerConfig", $$extension);
    if($$configFilename==""){
        //message "言語サーバの設定がIniファイル中で見つかりません";
        return "";
    }
    $$absConfigFileName=$rootFolder+"\\"+$$configFilename;
    if(! existfile($$absConfigFileName)){
        message "ファイルが見つからない。"+$$absConfigFileName;
        return "";
    }
    return $$absConfigFileName;


InitializeGlobalVariable:
    execmacro currentmacrodirectory+@"\expand_environment_strings.mac", $comClientFileName;
    $comClientFileName=getresultex(-1);

    execmacro currentmacrodirectory+@"\expand_environment_strings.mac", $logFileName;
    $logFileName=getresultex(-1);

    call GetAbsolutePathName $iniServerConfig;
    $iniServerConfig=$$return;
    return ;


CreateComObject:
    ##obj = createobject($comClientFileName,$comServiceName);
    if(##obj==0){
        message "COMのロードに失敗しました";
        return 0;
    }
    setcomdetachmethod ##obj,"Finalizer";
    return ##obj;


InitializeComObject:
    ##obj              =##1;
    $$absConfigFileName=$$2;
    ##n=member(##obj,"Initialize",$logFileName,$$absConfigFileName,directory2);
    if(##n==false){
        message "COMのInitializeに失敗";
        return false;
    }
    ##serverCapabilities=member(##obj,"ServerCapabilities");
    execmacro currentmacrodirectory+@"/LspServerCapabilities.mac", "Store", str(##serverCapabilities);
    keepobject ##obj, 2;
    return true;


GetAbsolutePathName:
	##fso=createobject("Scripting.FileSystemObject");
	$$abs_filename = member(##fso,"GetAbsolutePathName",$$1);
	releaseobject ##fso;
    return $$abs_filename;
