/*COMクライアントを作成する。

返値　成功：COM-Object(int)を文字列で返す
    　失敗：空文字
*/
/////////////////////////////////////////////////////////////////////////////
//    グローバル変数
/////////////////////////////////////////////////////////////////////////////

//
//LSP
//
$comClientFileName  =@"%HOMEDRIVE%%HOMEPATH%\GitHub\hidemaru_lsp_client\project\HidemaruLspClient_FrontEnd\bin\Debug\HidemaruLspClient_FrontEnd.dll";
$comServiceName     ="HidemaruLspClient_FrontEnd.Service";
$logFileName        =@"%HOMEDRIVE%%HOMEPATH%\hidemaru_lsp_client.log";
$privateComObjectName ="lsp.client.private.obj";
$publicComObjectName  ="lsp.client.public.obj";

$configName         ="lsp.client.configname";
#comShareMode       =2;

$labelIndexName="LabelIndex";
$labelTable[0]="InitializeGlobalVariable";
$labelTable[1]="CreateComObject";
$labelTable[2]="InitializeBackEndService";
$labelTable[3]="FindServerConfigFile";
$labelTable[4]="InitializeFrontEndService";
$labelTable[5]="Finish";
$labelTable[6]="Nop";


//
//Iniファイル
//
$rootFolder     =currentmacrodirectory+@"\..";
$iniServerConfig=$rootFolder+"\\server_config.ini";


/////////////////////////////////////////////////////////////////////////////
//    メイン処理
/////////////////////////////////////////////////////////////////////////////
call main;
endmacro getstaticvariable($publicComObjectName,#comShareMode);

main:
    $$index = getstaticvariable($labelIndexName,#comShareMode);
    if($$index==""){
        ##index=0;
    }else{
        ##index=val($$index);
    }
    call $labelTable[##index];
    if(! ##return){
        return ;
    }

    ##index = ##index + 1;
    setstaticvariable $labelIndexName,str(##index),#comShareMode;
    call main;
    return ;


FindServerConfigFile:
    $$extension = filetype;
    $$configFilename=getinistrw($iniServerConfig, "ServerConfig", $$extension);
    if($$configFilename==""){
        //message "言語サーバの設定がIniファイル中で見つかりません";
        return false;
    }
    $$absConfigFileName=$rootFolder+"\\"+$$configFilename;
    if(! existfile($$absConfigFileName)){
        message "ファイルが見つからない。"+$$absConfigFileName;
        return false;
    }
    setstaticvariable $configName,$$absConfigFileName,#comShareMode;
    return true;


InitializeGlobalVariable:
    execmacro currentmacrodirectory+@"\expand_environment_strings.mac", $comClientFileName;
    $comClientFileName=getresultex(-1);

    execmacro currentmacrodirectory+@"\expand_environment_strings.mac", $logFileName;
    $logFileName=getresultex(-1);

    call GetAbsolutePathName $iniServerConfig;
    $iniServerConfig=$$return;
    return true;


CreateComObject:
    ##obj = createobject($comClientFileName,$comServiceName);
    if(##obj==0){
        message "COMのロードに失敗しました";
        return false;
    }
    setstaticvariable $privateComObjectName,str(##obj),#comShareMode;
    setcomdetachmethod ##obj,"Finalizer";
    return true;

InitializeBackEndService:
    $$obj=getstaticvariable($privateComObjectName,#comShareMode);
    ##obj=val($$obj);
    ##n=member(##obj,"InitializeBackEndService",$logFileName);
    if(##n==false){
        message "[Failed] InitializeBackEndService";
        return false;
    }
    return true;

InitializeFrontEndService:
    $$obj=getstaticvariable($privateComObjectName,#comShareMode);
    ##obj=val($$obj);
    $$absConfigFileName=getstaticvariable($configName,#comShareMode);

    ##n=member(##obj,"InitializeFrontEndService",$$absConfigFileName,directory2);
    if(##n==false){
        message "[Failed] InitializeFrontEndService";
        return false;
    }
    ##serverCapabilities=member(##obj,"ServerCapabilities");
    execmacro currentmacrodirectory+@"/LspServerCapabilities.mac", "Store", str(##serverCapabilities);
    keepobject ##obj, 2;
    return true;

Finish:
    $$obj=getstaticvariable($privateComObjectName,#comShareMode);
    setstaticvariable $publicComObjectName,$$obj,#comShareMode;
    return true;

Nop:
    return false;


GetAbsolutePathName:
	##fso=createobject("Scripting.FileSystemObject");
	$$abs_filename = member(##fso,"GetAbsolutePathName",$$1);
	releaseobject ##fso;
    return $$abs_filename;
