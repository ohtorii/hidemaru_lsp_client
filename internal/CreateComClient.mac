/*COMクライアントを作成する。

返値　成功：COM-Object(int)を文字列で返す
    　失敗：空文字
*/
/////////////////////////////////////////////////////////////////////////////
//    グローバル変数
/////////////////////////////////////////////////////////////////////////////

#useAsync=true;

//
//LSP
//
$comService         ="HidemaruLspClient_FrontEnd.Service";
$comServiceAsync    ="HidemaruLspClient_FrontEnd.ServiceAsync";
$comServiceName     ="";

$privateComObjectName ="lsp.client.private.obj";
$publicComObjectName  ="lsp.client.public.obj";

#comShareMode       =2;

$labelIndexName="LabelIndex";
if(#useAsync){
    if(true){
        //Async
        $comServiceName=$comServiceAsync;
        $labelTable[0]="SpawnBackEndCOMServer";
        $labelTable[1]="CreateFrontEndCOM";
        $labelTable[2]="Initialize";
        $labelTable[3]="InitializeServiceAsync";
        $labelTable[4]="CheckService";
        $labelTable[5]="Finish";
        $labelTable[6]="Nop";
    }else{
        //Concurrent
        $comServiceName=$comService;
        $labelTable[0]="SpawnBackEndCOMServer";
        $labelTable[1]="CreateFrontEndCOM";
        $labelTable[2]="Initialize";
        $labelTable[3]="InitializeBackEndServiceAsync";
        $labelTable[4]="CheckBackEndService";
        $labelTable[5]="InitializeFrontEndServiceAsync";
        $labelTable[6]="CheckFrontEndService";
        $labelTable[7]="Finish";
        $labelTable[8]="Nop";
    }
}else{
    //Sync
    $comServiceName=$comService;
    $labelTable[0]="SpawnBackEndCOMServer";
    $labelTable[1]="CreateFrontEndCOM";
    $labelTable[2]="Initialize";
    $labelTable[3]="InitializeBackEndService";
    $labelTable[4]="InitializeFrontEndService";
    $labelTable[5]="Finish";
    $labelTable[6]="Nop";
}


//
//Iniファイル
//
$rootFolder     =currentmacrodirectory+@"\..";
$iniServerConfig=$rootFolder+"\\server_config.ini";


/////////////////////////////////////////////////////////////////////////////
//    メイン処理
/////////////////////////////////////////////////////////////////////////////
call main;
endmacro getstaticvariable($publicComObjectName,#comShareMode);

main:
    $$index = getstaticvariable($labelIndexName,#comShareMode);
    if($$index==""){
        ##index=0;
    }else{
        ##index=val($$index);
    }
    debuginfo 1;
    debuginfo $labelTable[##index];
    call $labelTable[##index];
    if(! ##return){
        return ;
    }

    ##index = ##index + 1;
    setstaticvariable $labelIndexName,str(##index),#comShareMode;
    call main;
    return ;

ShowComError:
    ##success=##1;
    ##hresult=##2;
    message sprintf("##success=%d / ##hresult=0x%x",##success, ##hresult);
    return ;

SpawnBackEndCOMServer:
    execmacro currentmacrodirectory+@"\GetBackEndExe.mac";
    $$comServerFileName=getresultex(-1);
    if(! existfile($$comServerFileName)){
        message "[BackEnd]ファイルが存在しません\nファイル名="+$$comServerFileName;
        return false;
    }

    //Memo: 標準入力が有効なのかすぐに終了する。
    //run $$comServerFileName + " -regserverperuser";

    runex $$comServerFileName + " --mode RegServerPerUser --start"
    		, 0 			//sync	  0:async 1:sync
    		, 0, "" 		//stdin   0:none 1:auto 2:file 3:(reserve) 4:all 5:select
    		, 0, "" 		//stdout  0:none 1:auto 2:file 3:add file  4:new 5:insert 6:replace
    		, 0, "" 		//stderr  0:none 1:=out 2:file 3:add file  4:new 5:insert 6:replace
    		, 0, "" 		//folder  0:none 1:current 2:specify 3:(reserve) 4:exe's folder
    		, 2 			//show	  0:auto 1:show 2:hide
    		, 1 			//nodraw  0:draw 1:no draw
    		, 0 			//unicode 0:ansi 2:unicode
    		;
    return result;

CreateFrontEndCOM:
    execmacro currentmacrodirectory+@"\GetFrontEndDLL.mac";
    $$comClientFileName=getresultex(-1);
    if(! existfile($$comClientFileName)){
        message "[FrontEnd]ファイルが存在しません\nファイル名="+$$comClientFileName;
        return false;
    }
    ##obj = createobject($$comClientFileName,$comServiceName);
    ##success=getresultex(10);
    ##hresult=getresultex(11);
    if(##obj==0){
        message sprintf("COMのロードに失敗しました\n##obj=%d / ##success=%d / ##hresult=0x%x\nFileName=%s",##obj, ##success, ##hresult, $$comClientFileName);
        return false;
    }
    setstaticvariable $privateComObjectName,str(##obj),#comShareMode;
    setcomdetachmethod ##obj,"Finalizer";
    keepobject ##obj, 2;
    return true;

Initialize:
    call GetAbsolutePathName $iniServerConfig;
    $$ini=$$return;

    $$obj=getstaticvariable($privateComObjectName,#comShareMode);
    ##obj=val($$obj);
    ##success=member(##obj,"Initialize",$$ini);
    if(! ##success){
        message "[Failed] Initialize"
        return false;
    }
    return true;

InitializeBackEndService:
    $$obj=getstaticvariable($privateComObjectName,#comShareMode);
    ##obj=val($$obj);
    ##n=member(##obj,"InitializeBackEndService");
    if(##n==false){
        message "[Failed] InitializeBackEndService";
        return false;
    }
    return true;

InitializeBackEndServiceAsync:
    $$obj=getstaticvariable($privateComObjectName,#comShareMode);
    ##obj=val($$obj);
    ##_=member(##obj,"InitializeBackEndServiceAsync");
    return true;

InitializeServiceAsync:
    $$obj=getstaticvariable($privateComObjectName,#comShareMode);
    ##obj=val($$obj);
    ##_=member(##obj,"InitializeServiceAsync",filetype,directory2);
    return true;

CheckService:
    $$obj=getstaticvariable($privateComObjectName,#comShareMode);
    ##obj=val($$obj);
    return member(##obj,"CheckService");

CheckBackEndService:
    $$obj=getstaticvariable($privateComObjectName,#comShareMode);
    ##obj=val($$obj);
    return member(##obj,"CheckBackEndService");

InitializeFrontEndService:
    $$obj=getstaticvariable($privateComObjectName,#comShareMode);
    ##obj=val($$obj);
    ##n=member(##obj,"InitializeFrontEndService",filetype,directory2);
    if(##n==false){
        message "[Failed] InitializeFrontEndService";
        return false;
    }
    return true;

InitializeFrontEndServiceAsync:
    $$obj=getstaticvariable($privateComObjectName,#comShareMode);
    ##obj=val($$obj);
    ##_=member(##obj,"InitializeFrontEndServiceAsync",filetype,directory2);
    return true;

CheckFrontEndService:
    $$obj=getstaticvariable($privateComObjectName,#comShareMode);
    ##obj=val($$obj);
    return member(##obj,"CheckFrontEndService");

Finish:
    $$obj=getstaticvariable($privateComObjectName,#comShareMode);
    setstaticvariable $publicComObjectName,$$obj,#comShareMode;
    return true;

Nop:
    return false;


GetAbsolutePathName:
	##fso=createobject("Scripting.FileSystemObject");
	$$abs_filename = member(##fso,"GetAbsolutePathName",$$1);
	releaseobject ##fso;
    return $$abs_filename;
