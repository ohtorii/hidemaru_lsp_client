/*COMクライアントを作成する。

返値　成功：COM-Object(int)を文字列で返す
    　失敗：空文字
*/
/////////////////////////////////////////////////////////////////////////////
//    グローバル変数
/////////////////////////////////////////////////////////////////////////////

//
//LSP
//
$comClientFileName =@"%HOMEDRIVE%%HOMEPATH%\GitHub\hidemaru_lsp_client\project\HidemaruLspClient_FrontEnd\bin\Debug\HidemaruLspClient_FrontEnd.dll";
$comServiceName    ="HidemaruLspClient_FrontEnd.Service";
$logFileName       =@"%HOMEDRIVE%%HOMEPATH%\hidemaru_lsp_client.log";
$comObjectName     ="lsp.client.obj";
#comObjectShareMode=2;
$comError          ="";

//
//Iniファイル
//
$rootFolder     =currentmacrodirectory+@"\..";
$iniServerConfig=$rootFolder+"\\server_config.ini";


/////////////////////////////////////////////////////////////////////////////
//    メイン処理
/////////////////////////////////////////////////////////////////////////////
call main;
endmacro $$return;


main:
    $$obj = getstaticvariable($comObjectName,#comObjectShareMode);
    if($$obj != ""){
        return $$obj;
    }
    call InitializeGlobalVariable;
    call FindServerConfigFile filetype;
    $$absConfigFileName=$$return;
    if($$absConfigFileName==""){
        message "言語サーバの設定がIniファイル中で見つかりません";
        return false;
    }

    if(! existfile($$absConfigFileName)){
        message "ファイルが見つからない。"+$$absConfigFileName;
        return $comError;
    }
    call InitializeComObject $$absConfigFileName;
    ##obj=##return;
    return str(##obj);


FindServerConfigFile:
    $$extension = $$1;
    $$configFilename=getinistrw($iniServerConfig, "ServerConfig", $$extension);
    if($$configFilename==""){
        return "";
    }
    $$absConfigFileName=$rootFolder+"\\"+$$configFilename;
    return $$absConfigFileName;


InitializeGlobalVariable:
    execmacro currentmacrodirectory+@"\expand_environment_strings.mac", $comClientFileName;
    $comClientFileName=getresultex(-1);

    execmacro currentmacrodirectory+@"\expand_environment_strings.mac", $logFileName;
    $logFileName=getresultex(-1);

    call GetAbsolutePathName $iniServerConfig;
    $iniServerConfig=$$return;
    return ;


InitializeComObject:
    $$absConfigFileName=$$1;
    ##obj = createobject($comClientFileName,$comServiceName);
    if(##obj==0){
        message "COMのロードに失敗しました";
        return $comError;
    }
    setcomdetachmethod ##obj,"Finalizer";
    ##n=member(##obj,"Initialize",$logFileName,$$absConfigFileName,directory2);
    if(##n==false){
        message "COMのInitializeに失敗";
        return $comError;
    }
    ##serverCapabilities=member(##obj,"ServerCapabilities");
    execmacro currentmacrodirectory+@"/LspServerCapabilities.mac", "Store", str(##serverCapabilities);

    setstaticvariable $comObjectName,str(##obj),#comObjectShareMode;
    keepobject ##obj, 2;
    return ;


GetAbsolutePathName:
	##fso=createobject("Scripting.FileSystemObject");
	$$abs_filename = member(##fso,"GetAbsolutePathName",$$1);
	releaseobject ##fso;
    return $$abs_filename;
