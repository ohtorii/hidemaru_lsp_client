if(version<891){
	message
	"秀丸エディタのバージョンが古いためマクロを実行できません。\n"	+
	"本マクロのドキュメントに記載されたバージョン以降の秀丸エディタをお使いください。";
	endmacro "0";
}

/////////////////////////////////////////////////////////////////////////////
//    グローバル変数
/////////////////////////////////////////////////////////////////////////////
$comClientFileName=@"%HOMEDRIVE%%HOMEPATH%\GitHub\hidemaru_lsp_client\project\HidemaruLspClient_FrontEnd\bin\Debug\HidemaruLspClient_FrontEnd.dll";
execmacro currentmacrodirectory+@"\expand_environment_strings.mac", $comClientFileName;
$comClientFileName=getresultex(-1);
$comServiceName="HidemaruLspClient_FrontEnd.Service";


$logFileName=@"%HOMEDRIVE%%HOMEPATH%\hidemaru_lsp_client.log";
execmacro currentmacrodirectory+@"\expand_environment_strings.mac", $logFileName;
$logFileName=getresultex(-1);


$rootFolder=currentmacrodirectory+@"\..";
$iniServerConfig=$rootFolder+"\\server_config.ini";
call GetAbsolutePathName $iniServerConfig;
$iniServerConfig=$$return;

$comObjectName="lsp.client.obj";
#comObjectShare=0;

/////////////////////////////////////////////////////////////////////////////
//    メイン処理
/////////////////////////////////////////////////////////////////////////////
call main;
endmacro str(##return);

main:
    call FindServerConfigFile filetype;
    $$absConfigFileName=$$return;
    if($$absConfigFileName==""){
        message "言語サーバの設定がIniファイル中で見つかりません";
        return false;
    }
    if(! existfile($$absConfigFileName)){
        message "ファイルが見つからない。"+$$absConfigFileName;
        return false;
    }

    debuginfo 1;
    $$obj = getstaticvariable($comObjectName,#comObjectShare);
    if($$obj!=""){
        ##obj = val($$obj);
    }else{
        ##obj = createobject($comClientFileName,$comServiceName);
        if(##obj==0){
            message "COMのロードに失敗しました";
            return false;
        }
        setstaticvariable $comObjectName,str(##obj),#comObjectShare;
        keepobject ##obj, 2;

        setcomdetachmethod ##obj,"Finalizer";
        ##n=callmethod_returnnum(##obj,"Initialize",$logFileName);
        if(##n==false){
            message "Initializeに失敗";
            return false;
        }
        ##n=callmethod_returnnum(##obj,"CreateWorker",$$absConfigFileName,directory2);
        if(##n==false){
            message "Start に失敗";
            return false;
        }
    }

    //Memo: -1して0始まりにする
    $$dictionalyFileName=callmethod_returnstr(##obj, "Completion",filename2,lineno-1,column_wcs);
    if($$dictionalyFileName==""){
        message "Completion に失敗";
        return false;
    }

    //Memo: マクロは中断される
    autocomplete 0, 0x00000002|0x00000004,0x00008020,$$dictionalyFileName;
    //Memo: マクロはこの行に到達しない

    //deletefile $$dictionalyFileName;
    return true;


FindServerConfigFile:
    $$extension = $$1;
    $$configFilename=getinistrw($iniServerConfig, "ServerConfig", $$extension);
    if($$configFilename==""){
        return "";
    }
    $$absConfigFileName=$rootFolder+"\\"+$$configFilename;
    return $$absConfigFileName;

GetAbsolutePathName:
	##fso=createobject("Scripting.FileSystemObject");
	$$abs_filename = member(##fso,"GetAbsolutePathName",$$1);
	releaseobject ##fso;
    return $$abs_filename;
