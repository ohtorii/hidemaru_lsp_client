if(version<891){
	message
	"秀丸エディタのバージョンが古いためマクロを実行できません。\n"	+
	"本マクロのドキュメントに記載されたバージョン以降の秀丸エディタをお使いください。";
	endmacro "0";
}

/////////////////////////////////////////////////////////////////////////////
//    グローバル変数
/////////////////////////////////////////////////////////////////////////////
$hidemaruLspClientDllPath=@"%HOMEDRIVE%%HOMEPATH%\GitHub\hidemaru_lsp_client\project\HidemaruLspClient\bin\Debug\x64\HidemaruLspClient.dll";
execmacro currentmacrodirectory+@"\expand_environment_strings.mac", $hidemaruLspClientDllPath;
$hidemaruLspClientDllPath=getresultex(-1);

#dllHidemaruLspClient=0;

$rootFolder=currentmacrodirectory+@"\..";
$iniServerConfig=$rootFolder+"\\server_config.ini";
call GetAbsolutePathName $iniServerConfig;
$iniServerConfig=$$return;


/////////////////////////////////////////////////////////////////////////////
//    メイン処理
/////////////////////////////////////////////////////////////////////////////
call main;
if(#dllHidemaruLspClient!=0){
    freedll(#dllHidemaruLspClient);
    #dllHidemaruLspClient=0;
}
endmacro str(##return);

main:
    call FindServerConfigFile filetype;
    $$absConfigFileName=$$return;
    if($$absConfigFileName==""){
        message "言語サーバの設定がIniファイル中で見つかりません";
        return false;
    }
    if(! existfile($$absConfigFileName)){
        message "ファイルが見つからない。"+$$absConfigFileName;
        return false;
    }

    debuginfo 1;
    debuginfo "$hidemaruLspClientDllPath="+$hidemaruLspClientDllPath;
    #dllHidemaruLspClient=loaddll($hidemaruLspClientDllPath);
	if(#dllHidemaruLspClient==0){
        message "HidemaruLspClient.dllのロードに失敗しました";
        return false;
    }
    ##n=dllfuncw(#dllHidemaruLspClient, "Start",$$absConfigFileName,directory2);
    if(##n==false){
        message "Start に失敗";
        return false;
    }
    //Memo: -1して0始まりにする
    $$dictionalyFileName=dllfuncstrw(#dllHidemaruLspClient, "Completion",filename2,lineno-1,column_wcs);
    if($$dictionalyFileName==""){
        message "Completion に失敗";
        return false;
    }

    //Memo: マクロは中断される
    autocomplete 0, 0x00000002|0x00000004,0x00008020,$$dictionalyFileName;
    //Memo: マクロはこの行に到達しない

    //deletefile $$dictionalyFileName;
    return true;


FindServerConfigFile:
    $$extension = $$1;
    $$configFilename=getinistrw($iniServerConfig, "ServerConfig", $$extension);
    if($$configFilename==""){
        return "";
    }
    $$absConfigFileName=$rootFolder+"\\"+$$configFilename;
    return $$absConfigFileName;

GetAbsolutePathName:
	##fso=createobject("Scripting.FileSystemObject");
	$$abs_filename = member(##fso,"GetAbsolutePathName",$$1);
	releaseobject ##fso;
    return $$abs_filename;
