if(version<900){
	message
	"秀丸エディタのバージョンが古いためマクロを実行できません。\n"	+
	"本マクロのドキュメントに記載されたバージョン以降の秀丸エディタをお使いください。";
	endmacro "0";
}

/////////////////////////////////////////////////////////////////////////////
//    グローバル変数
/////////////////////////////////////////////////////////////////////////////
$rootFolder=currentmacrodirectory+@"\..";
$iniServerConfig=$rootFolder+"\\server_config.ini";
call GetAbsolutePathName $iniServerConfig;
$iniServerConfig=$$return;


/////////////////////////////////////////////////////////////////////////////
//    メイン処理
/////////////////////////////////////////////////////////////////////////////
call main;
endmacro str(##return);

main:
    call FindServerConfigFile filetype;
    $$absConfigFileName=$$return;
    if($$absConfigFileName==""){
        message "言語サーバの設定がIniファイル中で見つかりません";
        return false;
    }
    if(! existfile($$absConfigFileName)){
        message "ファイルが見つからない。"+$$absConfigFileName;
        return false;
    }

    execmacro currentmacrodirectory+@"\CreateComClient.mac", $$absConfigFileName;
    $$obj = getresultex(-1);
    if($$obj==""){
        return false;
    }
    ##comObject=val($$obj);

    call CreateMenuArray;
    menuarray $g_menuName,#g_menuNum;
    if(result==0){
        return false;
    }
    call $g_menuJumpLabel[result-1],##comObject;
    return ##return;


CreateMenuArray:
    $g_menuName[0]     ="";
    #g_menuNum         =0;
    $g_menuJumpLabel[0]="";

    {
        $$capabilitiePrvides[0]="CompletionProvider"    ;
        $$capabilitiePrvides[1]="DeclarationProvider"   ;
        $$capabilitiePrvides[2]="DefinitionProvider"    ;
        $$capabilitiePrvides[3]="TypeDefinitionProvider";
        $$capabilitiePrvides[4]="ImplementationProvider";
        $$capabilitiePrvides[5]="ReferencesProvider"    ;
        ##capabilitiesNum=6;


        $$labels[0]="Completion"    ;
        $$labels[1]="Declaration"   ;
        $$labels[2]="Definition"    ;
        $$labels[3]="TypeDefinition";
        $$labels[4]="Implementation";
        $$labels[5]="References"    ;

        ##i=0;
        while(##i<##capabilitiesNum){
            execmacro currentmacrodirectory+@"\LspServerCapabilities.mac", "Load", $$capabilitiePrvides[##i];
            ##enable = val(getresultex(-1));
            if(##enable){
                $g_menuName[#g_menuNum]     =$$labels[##i];
                $g_menuJumpLabel[#g_menuNum]="Menu"+$$labels[##i];
                #g_menuNum                  = #g_menuNum + 1;
            }

            ##i = ##i + 1;
        }
    }

    {
        $g_menuName[#g_menuNum]     ="ファイル同期（開発用）";
        $g_menuJumpLabel[#g_menuNum]="MenuSyncDocument";
        #g_menuNum = #g_menuNum + 1;

    }
    return ;


MenuCompletion:
    ##comObject=##1;
    $$dictionalyFileName=member(##comObject,"Completion",lineno,column_wcs);
    if($$dictionalyFileName==""){
        message "Completion に失敗";
        return false;
    }
    //Memo: マクロは中断される
    autocomplete 0, 0x00000002|0x00000004,0x00008050,$$dictionalyFileName;

    //Memo: マクロはこの行に到達しない
    return true;

MenuDeclaration:
    call LspGoto "Declaration", ##1;
    return ##return;

MenuDefinition:
    call LspGoto "Definition", ##1;
    return ##return;

MenuTypeDefinition:
    call LspGoto "TypeDefinition", ##1;
    return ##return;

MenuImplementation:
    call LspGoto "Implementation", ##1;
    return ##return;

MenuReferences:
    call LspGoto "References", ##1;
    return ##return;

MenuSyncDocument:
    ##comObject=##1;
    ##success=member(##comObject, "SyncDocument");
    return ##success;


LspGoto:
    $$Method    =$$1;
    ##comObject =##2;
    execmacro currentmacrodirectory + @"/LspGoto.mac", $$Method, str(##comObject);
    return val(getresultex(-1));

FindServerConfigFile:
    $$extension = $$1;
    $$configFilename=getinistrw($iniServerConfig, "ServerConfig", $$extension);
    if($$configFilename==""){
        return "";
    }
    $$absConfigFileName=$rootFolder+"\\"+$$configFilename;
    return $$absConfigFileName;

GetAbsolutePathName:
	##fso=createobject("Scripting.FileSystemObject");
	$$abs_filename = member(##fso,"GetAbsolutePathName",$$1);
	releaseobject ##fso;
    return $$abs_filename;
